plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.javalin:javalin:4.6.4' // base server engine
    implementation 'org.apache.velocity:velocity-engine-core:2.3' // html template engine
    implementation 'ch.qos.logback:logback-classic:1.2.11' // logging implementation for slf4j
    implementation 'org.yaml:snakeyaml:1.30' // typed yaml support
    implementation 'com.google.code.gson:gson:2.9.1' // json support
    implementation 'com.unboundid:unboundid-ldapsdk:6.0.5' // ldap client for authentication
    implementation 'com.eatthepath:java-otp:0.4.0' // totp implementation
    implementation 'info.picocli:picocli:4.6.3' // cli options parser

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation 'io.javalin:javalin-testtools:4.6.4'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.33.2'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'
    testImplementation 'net.sourceforge.htmlunit:htmlunit:2.63.0'

    // lombok stuff
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

application {
    mainClass = 'com.github.lbarnkow.minchir.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('runMock', Test) {
    description 'Runs a mocked environment to work on frontend templates.'
    group 'application'

    environment "MOCKED_ENVIRONMENT", "ON"
    useJUnitPlatform()
}

tasks.register('integTest', Test) {
    description 'Runs the integration test suite.'
    group 'verification'

    environment "INTEGRATION_TEST", "ON"
    useJUnitPlatform()
}
